import Mathlib.Tactic
import Mathlib.Analysis.NormedSpace.Multilinear


open Classical

namespace ContinuousAlternatingMap

section Basic

universe u v w w‚ÇÇ

variable (R : Type u) (M : Type w) [Semiring R] [AddCommMonoid M]
[Module R M] [TopologicalSpace M] (N : Type w‚ÇÇ) [AddCommMonoid N] [Module R N] [TopologicalSpace N]
(Œπ : Type v)

structure ContinuousAlternatingMap extends AlternatingMap R M N Œπ where
cont : Continuous toFun

def toAlternatingMap (u : ContinuousAlternatingMap R M N Œπ) := u.toAlternatingMap


variable {R M N Œπ}

variable (f g : ContinuousAlternatingMap R M N Œπ)

instance funLike : FunLike (ContinuousAlternatingMap R M N Œπ) (Œπ ‚Üí M) (fun _ => N) where
  coe f := f.toFun
  coe_injective' := fun f g h ‚Ü¶ by
    rcases f with ‚ü®_, _‚ü©
    rcases g with ‚ü®_, _‚ü©
    congr
    simp only [MultilinearMap.toFun_eq_coe, AlternatingMap.coe_multilinearMap, FunLike.coe_fn_eq] at h
    exact h


instance coeFun : CoeFun (ContinuousAlternatingMap R M N Œπ) fun _ => (Œπ ‚Üí M) ‚Üí N :=
  ‚ü®FunLike.coe‚ü©

@[simp]
theorem toFun_eq_coe : f.toFun = f :=
  rfl

@[simp]
theorem coe_mk (f : AlternatingMap R M N Œπ) (h) :
    ‚áë(‚ü®f, h‚ü© : ContinuousAlternatingMap R M N Œπ) = f :=
  rfl

theorem congr_fun {f g : ContinuousAlternatingMap R M N Œπ} (h : f = g) (x : Œπ ‚Üí M) : f x = g x :=
  congr_arg (fun h : ContinuousAlternatingMap R M N Œπ => h x) h


theorem congr_arg (f : ContinuousAlternatingMap R M N Œπ) {x y : Œπ ‚Üí M} (h : x = y) : f x = f y :=
  _root_.congr_arg (fun x : Œπ ‚Üí M => f x) h

theorem coe_injective : Function.Injective ((‚Üë) : ContinuousAlternatingMap R M N Œπ ‚Üí (Œπ ‚Üí M) ‚Üí N) :=
  FunLike.coe_injective

@[norm_cast]
theorem coe_inj {f g : ContinuousAlternatingMap R M N Œπ} : (f : (Œπ ‚Üí M) ‚Üí N) = g ‚Üî f = g :=
  coe_injective.eq_iff

@[ext]
theorem ext {f f' : ContinuousAlternatingMap R M N Œπ} (H : ‚àÄ x, f x = f' x) : f = f' :=
  FunLike.ext _ _ H

theorem ext_iff {f g : ContinuousAlternatingMap R M N Œπ} : f = g ‚Üî ‚àÄ x, f x = g x :=
  ‚ü®fun h _ => h ‚ñ∏ rfl, fun h => ext h‚ü©

lemma toAlternatingMap_injective :
Function.Injective (ContinuousAlternatingMap.toAlternatingMap (R := R) (M := M) (N := N) (Œπ := Œπ)) := by
  intro f g heq
  ext x
  apply_fun (fun h => h.toFun) at heq
  change f.toFun = g.toFun at heq
  change f.toFun x = g.toFun x
  rw [heq]


def toContinuousMultilinearMap (m : ContinuousAlternatingMap R M N Œπ) :
ContinuousMultilinearMap R (fun (_ : Œπ) => M) N :=
{m.toAlternatingMap.toMultilinearMap with
 cont := m.cont}


lemma toContinuousMultilinearMap_injective :
Function.Injective (ContinuousAlternatingMap.toContinuousMultilinearMap (R := R) (M := M) (N := N) (Œπ := Œπ)) := by
  intro f g heq
  ext x
  apply_fun (fun h => h.toFun) at heq
  change f.toFun = g.toFun at heq
  change f.toFun x = g.toFun x
  rw [heq]


instance continuousMapClass : ContinuousMapClass (ContinuousAlternatingMap R M N Œπ) (Œπ ‚Üí M) N
    where
  coe f := f.toFun
  coe_injective' _ _ h := ContinuousAlternatingMap.coe_injective h
  map_continuous := ContinuousAlternatingMap.cont

def Simps.apply (L‚ÇÅ : ContinuousAlternatingMap R M N Œπ) (v : Œπ ‚Üí M) : N :=
  L‚ÇÅ v

initialize_simps_projections ContinuousAlternatingMap (-toMultilinearMap,
  toMultilinearMap_toFun ‚Üí apply)

@[continuity]
theorem coe_continuous : Continuous (f : (Œπ ‚Üí M) ‚Üí N) :=
  f.cont

@[simp]
theorem coe_coe : (f.toAlternatingMap : (Œπ ‚Üí M) ‚Üí N) = f :=
  rfl

variable (v : Œπ ‚Üí M)

@[simp]
theorem map_add [DecidableEq Œπ] (i : Œπ) (x y : M) :
    f (Function.update v i (x + y)) = f (Function.update v i x) + f (Function.update v i y) :=
  f.toAlternatingMap.map_add' v i x y


@[simp]
theorem map_smul [DecidableEq Œπ] (i : Œπ) (r : R) (x : M) :
    f (Function.update v i (r ‚Ä¢ x)) = r ‚Ä¢ f (Function.update v i x) :=
  f.toAlternatingMap.map_smul' v i r x

@[simp]
theorem map_eq_zero_of_eq (v : Œπ ‚Üí M) {i j : Œπ} (h : v i = v j) (hij : i ‚â† j) : f v = 0 :=
  f.map_eq_zero_of_eq' v i j h hij

theorem map_coord_zero {m : Œπ ‚Üí M} (i : Œπ) (h : m i = 0) : f m = 0 :=
  f.toAlternatingMap.map_coord_zero i h

@[simp]
theorem map_update_zero [DecidableEq Œπ] (m : Œπ ‚Üí M) (i : Œπ) : f (Function.update m i 0) = 0 :=
  f.toAlternatingMap.map_update_zero m i

@[simp]
theorem map_zero [Nonempty Œπ] : f 0 = 0 :=
  f.toAlternatingMap.map_zero

theorem map_eq_zero_of_not_injective (v : Œπ ‚Üí M) (hv : ¬¨Function.Injective v) : f v = 0 :=
f.toAlternatingMap.map_eq_zero_of_not_injective v hv

instance : Zero (ContinuousAlternatingMap R M N Œπ) :=
  ‚ü®{ (0 : AlternatingMap R M N Œπ) with cont := continuous_const }‚ü©

instance : Inhabited (ContinuousAlternatingMap R M N Œπ) :=
  ‚ü®0‚ü©

@[simp]
theorem zero_apply (m : Œπ ‚Üí M) : (0 : ContinuousAlternatingMap R M N Œπ) m = 0 :=
  rfl

@[simp]
theorem toContinuousMultilinearMap_zero : toContinuousMultilinearMap (0 : ContinuousAlternatingMap R M N Œπ) = 0 :=
  rfl

@[simp]
theorem toAlternatingMap_zero : (0 : ContinuousAlternatingMap R M N Œπ).toAlternatingMap = 0 :=
  rfl

section SMul

variable {R' R'' A : Type*} [Monoid R'] [Monoid R''] [Semiring A] [Module A M]
  [Module A N] [DistribMulAction R' N] [ContinuousConstSMul R' N] [SMulCommClass A R' N]
  [DistribMulAction R'' N] [ContinuousConstSMul R'' N] [SMulCommClass A R'' N]

instance : SMul R' (ContinuousAlternatingMap A M N Œπ) :=
  ‚ü®fun c f => { c ‚Ä¢ f.toAlternatingMap with cont := f.cont.const_smul c }‚ü©

@[simp]
theorem smul_apply (f : ContinuousAlternatingMap A M N Œπ) (c : R') (m : Œπ ‚Üí M) :
    (c ‚Ä¢ f) m = c ‚Ä¢ f m :=
  rfl

@[simp]
theorem toAlternatingMap_smul (c : R') (f : ContinuousAlternatingMap A M N Œπ) :
   (c ‚Ä¢ f).toAlternatingMap = c ‚Ä¢ f.toAlternatingMap :=
  rfl

@[simp]
theorem toContinuousMultilinearMap_smul (c : R') (f : ContinuousAlternatingMap A M N Œπ) :
   toContinuousMultilinearMap (c ‚Ä¢ f) = c ‚Ä¢ (toContinuousMultilinearMap f) :=
  rfl

instance [SMulCommClass R' R'' N] : SMulCommClass R' R'' (ContinuousAlternatingMap A M N Œπ) :=
  ‚ü®fun _ _ _ => ext fun _ => smul_comm _ _ _‚ü©

instance [SMul R' R''] [IsScalarTower R' R'' N] :
    IsScalarTower R' R'' (ContinuousAlternatingMap A M N Œπ) :=
  ‚ü®fun _ _ _ => ext fun _ => smul_assoc _ _ _‚ü©

instance [DistribMulAction R'·µê·µí·µñ N] [IsCentralScalar R' N] :
    IsCentralScalar R' (ContinuousAlternatingMap A M N Œπ) :=
  ‚ü®fun _ _ => ext fun _ => op_smul_eq_smul _ _‚ü©

instance : MulAction R' (ContinuousAlternatingMap A M N Œπ) :=
  Function.Injective.mulAction toContinuousMultilinearMap toContinuousMultilinearMap_injective fun _ _ => rfl

end SMul

section ContinuousAdd

variable [ContinuousAdd N]

instance : Add (ContinuousAlternatingMap R M N Œπ) :=
  ‚ü®fun f g => ‚ü®f.toAlternatingMap + g.toAlternatingMap, f.cont.add g.cont‚ü©‚ü©

@[simp]
theorem add_apply (m : Œπ ‚Üí M) : (f + g) m = f m + g m :=
  rfl


@[simp]
theorem toAlternatingMap_add (f g : ContinuousAlternatingMap R M N Œπ) :
    (f + g).toAlternatingMap = f.toAlternatingMap + g.toAlternatingMap :=
  rfl

@[simp]
theorem toContinuousMultilinearMap_add (f g : ContinuousAlternatingMap R M N Œπ) :
    toContinuousMultilinearMap (f + g) = toContinuousMultilinearMap f + toContinuousMultilinearMap g :=
  rfl

instance addCommMonoid : AddCommMonoid (ContinuousAlternatingMap R M N Œπ) :=
  toContinuousMultilinearMap_injective.addCommMonoid _ rfl (fun _ _ => rfl) fun _ _ => rfl


def toContinuousMultilinearMapAddMonoidHom : AddMonoidHom (ContinuousAlternatingMap R M N Œπ)
(ContinuousMultilinearMap R (fun (_ : Œπ) => M) N) :=
{toFun := toContinuousMultilinearMap
 map_zero' := toContinuousMultilinearMap_zero
 map_add' := toContinuousMultilinearMap_add}

/-- Evaluation of a `ContinuousAlternatingMap` at a vector as an `AddMonoidHom`. -/
def applyAddHom (m : Œπ ‚Üí M) : ContinuousAlternatingMap R M N Œπ ‚Üí+ N where
  toFun f := f m
  map_zero' := rfl
  map_add' _ _ := rfl

@[simp]
theorem sum_apply {Œ± : Type*} (f : Œ± ‚Üí ContinuousAlternatingMap R M N Œπ) (m : Œπ ‚Üí M)
    {s : Finset Œ±} : (Finset.sum s f) m = Finset.sum s (fun a => f a m) :=
  (applyAddHom m).map_sum f s

end ContinuousAdd

section SubNeg

variable {R' M' N' : Type*} [Ring R'] [AddCommGroup M'] [AddCommGroup N']
[Module R' M'] [Module R' N'] [TopologicalSpace M'] [TopologicalSpace N']
(v' : Œπ ‚Üí M') (f' g' : ContinuousAlternatingMap R' M' N' Œπ)

@[simp]
theorem map_sub [DecidableEq Œπ] (i : Œπ) (x y : M') :
    g' (Function.update v' i (x - y)) = g' (Function.update v' i x) - g' (Function.update v' i y) :=
  g'.toAlternatingMap.map_sub v' i x y


@[simp]
theorem map_neg [DecidableEq Œπ] (i : Œπ) (x : M') : g' (Function.update v' i (-x)) = -g' (Function.update v' i x) :=
  g'.toAlternatingMap.map_neg v' i x

variable [TopologicalAddGroup N']

instance : Neg (ContinuousAlternatingMap R' M' N' Œπ) :=
  ‚ü®fun f => { -f.toAlternatingMap with cont := f.cont.neg }‚ü©

@[simp]
theorem neg_apply (m : Œπ ‚Üí M') : (-g') m = -g' m :=
  rfl

instance subContinuousAlternatingMap : Sub (ContinuousAlternatingMap R' M' N' Œπ) :=
  ‚ü®fun f g => { f.toAlternatingMap - g.toAlternatingMap with cont := f.cont.sub g.cont }‚ü©

@[simp]
theorem sub_apply (m : Œπ ‚Üí M') : (f' - g') m = f' m - g' m :=
  rfl

instance : AddCommGroup (ContinuousAlternatingMap R' M' N' Œπ) :=
  toContinuousMultilinearMap_injective.addCommGroup _ rfl (fun _ _ => rfl) (fun _ => rfl) (fun _ _ => rfl)
    (fun _ _ => rfl) fun _ _ => rfl


end SubNeg

section DistribMulAction

variable {R' R'' A : Type*} [Monoid R'] [Monoid R''] [Semiring A] [Module A M]
  [Module A N] [DistribMulAction R' N] [ContinuousConstSMul R' N] [SMulCommClass A R' N]
  [DistribMulAction R'' N] [ContinuousConstSMul R'' N] [SMulCommClass A R'' N]


instance [ContinuousAdd N] : DistribMulAction R' (ContinuousAlternatingMap A M N Œπ) :=
  Function.Injective.distribMulAction
    { toFun := toContinuousMultilinearMap,
      map_zero' := toContinuousMultilinearMap_zero,
      map_add' := toContinuousMultilinearMap_add }
    toContinuousMultilinearMap_injective
    fun _ _ => rfl

end DistribMulAction

section Module

variable {R' A : Type*} [Semiring R'] [Semiring A] [ContinuousAdd N] [Module A M]
  [Module A N] [Module R' N] [ContinuousConstSMul R' N] [SMulCommClass A R' N]

/-- The space of continuous alternating maps over an algebra over `R` is a module over `R`, for the
pointwise addition and scalar multiplication. -/
instance : Module R' (ContinuousAlternatingMap A M N Œπ) :=
  Function.Injective.module _
    { toFun := toContinuousMultilinearMap,
      map_zero' := toContinuousMultilinearMap_zero,
      map_add' := toContinuousMultilinearMap_add }
    toContinuousMultilinearMap_injective fun _ _ => rfl

/-- Linear map version of the map `toAlternatingMap` associating to a continuous alternating map
the corresponding alternating map. -/
@[simps!]
def toAlternatingMapLinear : ContinuousAlternatingMap A M N Œπ ‚Üí‚Çó[R'] AlternatingMap A M N Œπ where
  toFun f := f.toAlternatingMap
  map_add' := toAlternatingMap_add
  map_smul' := toAlternatingMap_smul

/-- Linear map version of the map `toContinuousMultilinearMap` associating to a continuous alternating map
the corresponding continuous multilinear map. -/
def toContinuousMultilinearMapLinear : ContinuousAlternatingMap A M N Œπ ‚Üí‚Çó[R']
ContinuousMultilinearMap A (fun (_ : Œπ) => M) N where
  toFun := toContinuousMultilinearMap
  map_add' := toContinuousMultilinearMap_add
  map_smul' := toContinuousMultilinearMap_smul

end Module

@[simps! toMultilinearMap apply_coe]
def codRestrict (f : ContinuousAlternatingMap R M N Œπ) (p : Submodule R N) (h : ‚àÄ v, f v ‚àà p) :
    ContinuousAlternatingMap R M p Œπ :=
  ‚ü®f.1.codRestrict p h, f.cont.subtype_mk _‚ü©

/- If `g` is continuous alternating and `f` is a continuous linear maps,
then `g (f m‚ÇÅ, ..., f m‚Çô)` is again a continuous alternating map, that we call
`g.compContinuousLinearMap f`. -/
variable {P} [AddCommMonoid P] [Module R P] [TopologicalSpace P]

def compContinuousLinearMap (g : ContinuousAlternatingMap R M N Œπ)
    (f : P ‚ÜíL[R] M) : ContinuousAlternatingMap R P N Œπ :=
  { g.toAlternatingMap.compLinearMap f.toLinearMap with
    cont := g.cont.comp <| continuous_pi fun _ => f.cont.comp <| continuous_apply _ }

@[simp]
theorem compContinuousLinearMap_apply (g : ContinuousAlternatingMap R M N Œπ)
    (f : P ‚ÜíL[R] M) (p : Œπ ‚Üí P) :
    compContinuousLinearMap g f p = g (fun i => f (p i)) :=
    rfl


/-- Composing a continuous alternating map with a continuous linear map gives again a
continuous alternating map. -/
def _root_.ContinuousLinearMap.compContinuousAlternatingMap (g : N ‚ÜíL[R] P)
    (f : ContinuousAlternatingMap R M N Œπ) : ContinuousAlternatingMap R M P Œπ :=
  { g.toLinearMap.compAlternatingMap f.toAlternatingMap with cont := g.cont.comp f.cont }

@[simp]
theorem _root_.ContinuousLinearMap.compContinuousAlternatingMap_coe (g : N ‚ÜíL[R] P)
    (f : ContinuousAlternatingMap R M N Œπ) :
    (g.compContinuousAlternatingMap f : (Œπ ‚Üí M) ‚Üí P) =
      (g : N ‚Üí P) ‚àò (f : (Œπ ‚Üí M) ‚Üí N) := by
  ext m
  rfl

end Basic

section Norm

variable {ùïú Œπ E F : Type*} [Fintype Œπ] [NontriviallyNormedField ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E]
[NormedAddCommGroup F] [NormedSpace ùïú F]

noncomputable instance instSemiNormedAddCommGroupContinuousAlternatingMap : SeminormedAddCommGroup
(ContinuousAlternatingMap ùïú E F Œπ) :=
SeminormedAddCommGroup.induced (ContinuousAlternatingMap ùïú E F Œπ) (ContinuousMultilinearMap ùïú (fun (_ : Œπ) => E) F)
toContinuousMultilinearMapAddMonoidHom


noncomputable instance instNormedSpaceContinuousAlternatingMap : NormedSpace ùïú (ContinuousAlternatingMap ùïú E F Œπ) :=
NormedSpace.induced ùïú (ContinuousAlternatingMap ùïú E F Œπ) (ContinuousMultilinearMap ùïú (fun (_ : Œπ) => E) F)
toContinuousMultilinearMapLinear

end Norm

end ContinuousAlternatingMap
