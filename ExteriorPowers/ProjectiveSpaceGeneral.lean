import ExteriorPowers.ClosedHyperplanes
import Mathlib.Geometry.Manifold.Instances.Sphere


open scoped LinearAlgebra.Projectivization
open Classical
noncomputable section

universe u

/- Manifold structure on E-{0}.-/

variable {ùïú E : Type u} [NontriviallyNormedField ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E]
  [CompleteSpace ùïú]

lemma EstarIsOpen:  IsOpen {u : E | u ‚â† 0} :=
isOpen_compl_iff.mpr (isClosed_singleton)

def EstarToE : OpenEmbedding (fun (u : {u : E // u ‚â† 0}) => (u : E)) :=
{
  induced := by tauto
  inj := by intro u v; rw [SetCoe.ext_iff]; simp only [imp_self]
  open_range := by simp only [Subtype.range_coe_subtype, Set.setOf_mem_eq]
                   exact EstarIsOpen
}

variable [Nonempty {u : E // u ‚â† 0}]


lemma OpenEmbeddingEstar.inverse {u : E} (hu : u ‚â† 0) :
u = (OpenEmbedding.toPartialHomeomorph (fun u => u.1) (EstarToE)).symm u := by
  have heq : u = (fun u=> u.1) (‚ü®u, hu‚ü© : {u : E // u ‚â† 0}) := by simp only
  nth_rewrite 2 [heq]
  nth_rewrite 2 [‚Üê(OpenEmbedding.toPartialHomeomorph_apply _ EstarToE)]
  rw [PartialHomeomorph.left_inv]
  tauto


instance instChartedSpaceEstar : ChartedSpace E {u : E // u ‚â† 0} := EstarToE.singletonChartedSpace

lemma Estar.chartAt (u : {u : E // u ‚â† 0}) :
instChartedSpaceEstar.chartAt u = OpenEmbedding.toPartialHomeomorph (fun u => u.1) EstarToE := by tauto

lemma Estar.chartAt.target (u : {u : E // u ‚â† 0}) :
(instChartedSpaceEstar.2 u).toPartialEquiv.target = {u : E // u ‚â† 0} := by
  rw [Estar.chartAt, OpenEmbedding.toPartialHomeomorph_target]
  simp only [ne_eq, Set.coe_setOf, Set.mem_setOf_eq, Subtype.range_coe_subtype]


lemma Estar.chartAt.inverse (u : {u : E // u ‚â† 0}) {v : E} (hv : v ‚â† 0) :
v = (instChartedSpaceEstar.chartAt u).symm v := by
  rw [Estar.chartAt]
  exact OpenEmbeddingEstar.inverse hv

instance : SmoothManifoldWithCorners (modelWithCornersSelf ùïú E) {u : E // u ‚â† 0} :=
  EstarToE.singleton_smoothManifoldWithCorners (modelWithCornersSelf ùïú E)



section Preliminaries

/- Some lemmas about linear forms.-/

lemma NonzeroOfNonzeroPhi {œÜ : E ‚Üí‚Çó[ùïú] ùïú} {u : E} (hu :œÜ u ‚â† 0) : u ‚â† 0 := by
  by_contra habs
  rw [habs] at hu
  simp only [map_zero, ne_eq, not_true] at hu

lemma NonzeroPhiOfPhiEqOne {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) : œÜ ‚â† 0 := by
  by_contra habs
  rw [habs] at hv
  simp only [ContinuousLinearMap.zero_apply, zero_ne_one] at hv

lemma NonzeroExistsEqOne {œÜ : E‚ÜíL[ùïú] ùïú} (hœÜ : œÜ ‚â† 0) : ‚àÉ (v : E), œÜ v = 1 := by
  match ContinuousLinearMap.exists_ne_zero hœÜ with
  | ‚ü®u, hu‚ü© =>
    existsi (1 / œÜ u) ‚Ä¢ u
    simp only [one_div, map_smul, smul_eq_mul, ne_eq]
    rw [mul_comm]
    simp only [ne_eq, hu, not_false_eq_true, mul_inv_cancel]

lemma Projectivization_vs_LinearMap {F : Type u} [AddCommMonoid F] [Module ùïú F] (œÜ : E ‚Üí‚Çó[ùïú] ùïú) {u v : E}
(hu : u ‚â† 0) (hv : v ‚â† 0) (f : E ‚Üí‚Çó[ùïú] F)
(hproj : Projectivization.mk ùïú u hu = Projectivization.mk ùïú v hv) :
(1 / (œÜ u)) ‚Ä¢ (f u) = (1 / (œÜ v)) ‚Ä¢ (f v) := by
  rw [Projectivization.mk_eq_mk_iff] at hproj
  match hproj with
  | ‚ü®a, ha‚ü© =>
    change (a.1) ‚Ä¢ v = u at ha
    rw [‚Üêha]
    simp only [map_smul, smul_eq_mul, one_div, mul_inv_rev]
    rw [smul_smul, mul_assoc]
    simp only [ne_eq, Units.ne_zero, not_false_eq_true, inv_mul_cancel, mul_one]

lemma Projectivization_vs_LinearMap_cor (œÜ : E ‚Üí‚Çó[ùïú] ùïú) {u v : E} (hu : u ‚â† 0) (hv : v ‚â† 0)
(hproj : Projectivization.mk ùïú u hu = Projectivization.mk ùïú v hv) :
(1 / (œÜ u)) ‚Ä¢ u = (1 / (œÜ v)) ‚Ä¢ v :=
Projectivization_vs_LinearMap œÜ hu hv (LinearMap.id) hproj

/-- Equip projective space `‚Ñô ùïú E` with the "coinduced" topology from the natural map
`mk' : E ‚àñ {0} ‚Üí ‚Ñô ùïú E`. -/
instance : TopologicalSpace (‚Ñô ùïú E) :=
TopologicalSpace.coinduced (Projectivization.mk' ùïú) instTopologicalSpaceSubtype

/- We introduce the open sets where the charts will be defined.-/

def Goodset (œÜ : E ‚ÜíL[ùïú] ùïú) : Set (‚Ñô ùïú E) := {x | œÜ x.rep ‚â† 0}

lemma GoodsetZero {œÜ : E ‚ÜíL[ùïú] ùïú} (hœÜ : œÜ = 0) : Goodset œÜ = ‚àÖ := by
  unfold Goodset
  rw [hœÜ]
  simp only [ContinuousLinearMap.zero_apply, ne_eq, not_true, Set.setOf_false]

/- Preimage of the good set in E-{0}.-/

lemma GoodsetPreimage (œÜ : E ‚ÜíL[ùïú] ùïú) {u : E} (hu : u ‚â† 0) :
(œÜ u ‚â† 0) ‚Üî Projectivization.mk ùïú u hu ‚àà Goodset œÜ := by
  set x := Projectivization.mk ùïú u hu
  have hux : Projectivization.mk ùïú u hu = Projectivization.mk ùïú x.rep (Projectivization.rep_nonzero x) :=
    by simp only [Projectivization.mk_rep]
  match (Projectivization.mk_eq_mk_iff ùïú u x.rep hu (Projectivization.rep_nonzero x)).mp hux with
  | ‚ü®a, ha‚ü© =>
    change (a.1)‚Ä¢x.rep = u at ha
    constructor
    . intro h
      change œÜ x.rep ‚â† 0
      by_contra habs
      rw [‚Üêha, map_smul, smul_eq_mul, habs, mul_zero] at h
      exact h (Eq.refl 0)
    . intro h
      rw [‚Üêha, map_smul, smul_eq_mul]
      by_contra habs
      apply_fun (fun x => (a.1)‚Åª¬π*x) at habs
      simp only [Units.isUnit, IsUnit.inv_mul_cancel_left, mul_zero] at habs
      exact h habs


lemma NonzeroPhiIsOpen' (œÜ : E ‚ÜíL[ùïú] ùïú) : IsOpen {u : {w : E | w ‚â† 0} | œÜ u ‚â† 0} := by
  have heq : {u : {w : E | w ‚â† 0} | œÜ u ‚â† 0} = ({w : E | w ‚â† 0}.restrict œÜ.toLinearMap)‚Åª¬π'
    {a : ùïú | a ‚â† 0} := by
      simp only [ne_eq, Set.coe_setOf, Set.mem_setOf_eq, ContinuousLinearMap.coe_coe, Set.preimage_setOf_eq,
        Set.restrict_apply]
  have hcont : Continuous ({w : E | w ‚â† 0}.restrict œÜ.toLinearMap) := Continuous.comp œÜ.cont continuous_subtype_val
  rw [heq]
  refine continuous_def.mp hcont _ ?_
  exact EstarIsOpen

lemma NonzeroPhiIsOpen (œÜ : E ‚ÜíL[ùïú] ùïú) : IsOpen {u : E | œÜ u ‚â† 0} := by
  rw [‚Üê(@Set.preimage_setOf_eq _ _ (fun u => u ‚â† 0) œÜ)]
  apply continuous_def.mp œÜ.cont
  exact EstarIsOpen


lemma GoodsetIsOpen (œÜ : E ‚ÜíL[ùïú] ùïú) : IsOpen (Goodset œÜ) := by
  apply isOpen_coinduced.mpr
  have heq : (Projectivization.mk' ùïú)‚Åª¬π' (Goodset œÜ) = {u | œÜ u.1 ‚â† 0} := by
    ext u
    simp only [ne_eq, Set.mem_preimage, Projectivization.mk'_eq_mk, Set.mem_setOf_eq]
    exact (Iff.symm (GoodsetPreimage œÜ u.2))
  rw [heq]
  exact NonzeroPhiIsOpen' œÜ



end Preliminaries

namespace ProjectiveSpace

section Chart


/- First direction: from projective space to a hyperplane in E.-/


/- If œÜ is a continuous linear form on E and v is a vector of E such that œÜ(v)=1, we define
a chart on Goodset œÜ with image in the hyperplane Ker(œÜ). We assume that œÜ(v)=1 (and not just
œÜ(v) ‚â† 0), because it suffices and simplifies the formulas. For technical reasons, the chart is
defined on all of ‚Ñô(E). If x is in Goodset œÜ, the last step (the retraction) does
nothing as the image of x by the first map is already in Ker(œÜ). -/


def Chart {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) (x : ‚Ñô ùïú E) : LinearMap.ker œÜ :=
(ContinuousRetractionOnHyperplane hv) ((œÜ v / œÜ x.rep) ‚Ä¢ x.rep - v)



/- To prove continuity, we lift the chart to a map defined on E.-/


def ChartLift_aux (œÜ : E ‚ÜíL[ùïú] ùïú) (v : E) (u : E) : E  :=
(œÜ v / œÜ u) ‚Ä¢ u - v

def ChartLift {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) : E ‚Üí (LinearMap.ker œÜ) :=
(ContinuousRetractionOnHyperplane hv) ‚àò (ChartLift_aux œÜ v)


lemma ChartLift_is_lift {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) {u : E} (hu : u ‚â† 0) :
ChartLift hv u = Chart hv (Projectivization.mk ùïú u hu) := by
  unfold ChartLift Chart ChartLift_aux
  simp only [hv, Function.comp_apply, map_sub, map_smul, sub_left_inj]
  apply Projectivization_vs_LinearMap
  exact Eq.symm (Projectivization.mk_rep (Projectivization.mk ùïú u hu))


/- We prove that this lift is smooth. For this we need ùïú to be complete (to prove smoothness
of quotients of smooth functions). -/


lemma ChartLift_aux_IsSmoothAt (œÜ : E‚ÜíL[ùïú]ùïú)  (v : E) {u : E} (hu :œÜ u ‚â† 0) :
ContDiffAt ùïú ‚ä§ (ChartLift_aux œÜ v) u := by
  unfold ChartLift_aux
  apply ContDiffAt.sub
  . apply ContDiffAt.smul
    . apply ContDiffAt.div
      . apply contDiffAt_const
      . apply contDiff_iff_contDiffAt.mp
        apply ContinuousLinearMap.contDiff
      . exact hu
    . apply contDiffAt_id
  . apply contDiffAt_const

lemma ChartLift_IsSmoothAt {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) {u : E} (hu :œÜ u ‚â† 0) :
ContDiffAt ùïú ‚ä§ (ChartLift hv) u := by
  unfold ChartLift
  apply ContDiffAt.continuousLinearMap_comp
  exact ChartLift_aux_IsSmoothAt œÜ v hu

/- We deduce that the lift is continuous. -/

lemma ChartLift_IsContinuousAt {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) {u : E} (hu :œÜ u ‚â† 0) :
ContinuousAt (ChartLift hv) u :=
@ContDiffAt.continuousAt ùïú _ E _ _ (LinearMap.ker œÜ) _ _ _ u ‚ä§ (ChartLift_IsSmoothAt hv hu)

lemma ChartLift_IsContinuous {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1)  :
ContinuousOn (ChartLift hv) {u : E | œÜ u ‚â† 0} :=
ContinuousAt.continuousOn (fun _ hu => ChartLift_IsContinuousAt hv hu)


/- Now we deduce that the chart itself is continuous. -/

lemma Chart_IsContinuous {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) :
ContinuousOn (Chart hv) (Goodset œÜ) := by
  apply (continuousOn_open_iff (GoodsetIsOpen œÜ)).mpr
  intro U hU
  apply isOpen_coinduced.mpr
  have heq : ((Projectivization.mk' ùïú) ‚Åª¬π'
   (Goodset œÜ ‚à© (fun (x : ‚Ñô ùïú E) => Chart hv x) ‚Åª¬π' U)) = (fun u => u.1)‚Åª¬π'
   ({u : E| (œÜ u ‚â† 0)} ‚à© (ChartLift hv)‚Åª¬π' U) := by
    ext u
    simp only [ne_eq, Set.preimage_inter, Set.mem_inter_iff, Set.mem_preimage, Projectivization.mk'_eq_mk,
      Set.preimage_setOf_eq, Set.mem_setOf_eq]
    rw [‚ÜêGoodsetPreimage, ChartLift_is_lift]
  rw [heq]
  apply IsOpen.preimage
  . simp_all only [ne_eq, Set.preimage_inter, Set.preimage_setOf_eq]
    apply continuous_induced_dom
  . exact (@continuousOn_open_iff _ _ _ _ (ChartLift hv) _ (NonzeroPhiIsOpen œÜ)).mp (ChartLift_IsContinuous hv) U hU

end Chart


/- Now we construct the charts in the other direction. -/

section InverseChart

lemma InverseChart_nonzero {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) (u : LinearMap.ker œÜ) :
œÜ (v + u) ‚â† 0 := by
  rw [map_add, u.2, hv, add_zero]
  exact one_ne_zero

def InverseChart {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) (u : LinearMap.ker œÜ) : ‚Ñô ùïú E :=
Projectivization.mk ùïú (v + u.1) (NonzeroOfNonzeroPhi (InverseChart_nonzero hv u))

lemma InverseChart_CodomainGoodset {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) (u : LinearMap.ker œÜ) :
(InverseChart hv u) ‚àà (Goodset œÜ) := by
  unfold InverseChart
  rw [‚ÜêGoodsetPreimage]
  exact InverseChart_nonzero hv u

-- Is this useful ?
lemma InverseChart_GoodsetPreimage (œÜ : E ‚ÜíL[ùïú] ùïú) {œà : E ‚ÜíL[ùïú] ùïú} {w : E} (hw : œà w = 1)
(u : LinearMap.ker œà) : (InverseChart hw u) ‚àà (Goodset œÜ) ‚Üî (œÜ (w + u) ‚â† 0) := by
  unfold InverseChart
  apply Iff.symm
  refine GoodsetPreimage œÜ ?_



/- Proof of the continuity of the inverse chart. First we lift it to a map with codomain E. -/

def InverseChart_lift (œÜ : E ‚ÜíL[ùïú] ùïú) (v : E) (u : LinearMap.ker œÜ) := v + u


/- We prove that this lift is smooth. -/

lemma InverseChart_lift_IsSmooth (œÜ : E ‚ÜíL[ùïú] ùïú) (v : E) :
ContDiff ùïú ‚ä§ (InverseChart_lift œÜ v) := by
  apply ContDiff.add
  . exact contDiff_const
  . set f : LinearMap.ker œÜ ‚ÜíL[ùïú] E :=
    {toFun := fun u => u.1
     map_smul' := by tauto
     map_add' := by tauto
     cont := by continuity
    }
    exact ContinuousLinearMap.contDiff f



/- We deduce that the lift is continuous. -/

lemma InverseChart_lift_IsContinuous (œÜ : E ‚ÜíL[ùïú] ùïú) (v : E) :
Continuous (InverseChart_lift œÜ v) :=
ContDiff.continuous (InverseChart_lift_IsSmooth œÜ v)

/- To relate this to the inverse chart, it is convenient to define a variant of the lift with codomain {u : E | u ‚â† 0}.-/

def InverseChart_lift' {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) :=
Set.codRestrict (InverseChart_lift œÜ v) {u : E | u ‚â† 0}
(fun u => NonzeroOfNonzeroPhi (InverseChart_nonzero hv u))

lemma InverseChart_lift'_IsLift {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) :
InverseChart hv = (Projectivization.mk' ùïú) ‚àò (InverseChart_lift' hv) := by
  ext u
  unfold InverseChart InverseChart_lift' InverseChart_lift
  simp only [ne_eq, Function.comp_apply, Projectivization.mk'_eq_mk, Set.val_codRestrict_apply]

lemma InverseChart_lift'_IsContinuous {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) :
Continuous (InverseChart_lift' hv) :=
Continuous.codRestrict (InverseChart_lift_IsContinuous œÜ v) _

/- We deduce that the inverse chart is continuous. -/

lemma InverseChart_IsContinuous {œÜ : E ‚ÜíL[ùïú] ùïú}  {v : E} (hv : œÜ v = 1) :
Continuous (InverseChart hv) :=
Continuous.comp continuous_coinduced_rng (InverseChart_lift'_IsContinuous hv)

end InverseChart


/- We need to prove that the chart and the inverse chart are indeed inverses of each other.-/

section Charts_are_inverses

/- We prove that the chart without the retraction sends the goodset of œÜ to Ker(œÜ). -/

lemma ChartCodomainEqDomainInverseChart {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) {x : ‚Ñô ùïú E} (hx : x ‚àà Goodset œÜ) :
œÜ (((1 / œÜ x.rep) ‚Ä¢ x.rep - v)) = 0 := by
  simp only [one_div, map_sub, map_smul, smul_eq_mul, ne_eq]
  rw [mul_comm _ (œÜ x.rep), DivisionRing.mul_inv_cancel, hv, sub_self]
  exact hx


/- We prove that InverseChart (Chart x) is x if x is in the Goodset of œÜ. -/

lemma InverseChartChart {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) {x : ‚Ñô ùïú E} (hx : x ‚àà Goodset œÜ) :
InverseChart hv (Chart hv x) = x := by
  unfold Chart InverseChart
  rw [hv]
  have heq : ‚Üë(ContinuousRetractionOnHyperplane hv ((1 / œÜ x.rep) ‚Ä¢ x.rep - v)) = ((1 / œÜ x.rep) ‚Ä¢ x.rep - v) := by
    rw [ContinuousRetractionIsRetraction hv ‚ü®_, ChartCodomainEqDomainInverseChart hv hx‚ü©]
  simp_rw [heq]
  simp only [add_sub_cancel'_right]
  have hnz : 1 / (œÜ x.rep) ‚â† 0 := by
    rw [div_eq_mul_inv, one_mul]
    by_contra habs
    apply_fun (fun a => (œÜ x.rep) * a) at habs
    simp only [ne_eq, mul_zero, mul_eq_zero, inv_eq_zero, or_self] at habs
    exact hx habs
  have hnz' : (1 / (œÜ x.rep)) ‚Ä¢ x.rep ‚â† 0 := smul_ne_zero hnz (Projectivization.rep_nonzero x)
  suffices Projectivization.mk ùïú ((1 / (œÜ x.rep)) ‚Ä¢ x.rep) hnz' = Projectivization.mk ùïú
    x.rep (Projectivization.rep_nonzero x) by exact Eq.trans this (Projectivization.mk_rep x)
  apply (Projectivization.mk_eq_mk_iff ùïú _ _ hnz' (Projectivization.rep_nonzero x)).mpr
  existsi Units.mk0 (1 / (œÜ x.rep)) hnz
  simp only [Units.smul_mk0]


/- We prove that the inverse chart sends Ker(œÜ) to the Goodset of œÜ. -/

lemma InverseChartCodomainEqDomainChart {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) (u : LinearMap.ker œÜ) :
(InverseChart hv u) ‚àà (Goodset œÜ) := by
  unfold InverseChart
  apply (GoodsetPreimage œÜ _).mp
  exact InverseChart_nonzero hv u


/-Now we prove that Chart (InverseChart u) is u. -/

lemma ChartInverseChart {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv : œÜ v = 1) (u : LinearMap.ker œÜ) :
Chart hv (InverseChart hv u) = u := by
  have hœÜ1 : œÜ (v + u.1) = 1 := by
    rw [map_add, hv, u.2, add_zero]
  have hœÜ : œÜ (v + u.1) ‚â† 0 := by
    rw [hœÜ1]
    simp only [ne_eq, one_ne_zero, not_false_eq_true]
  have hvu : v + u.1 ‚â† 0 := NonzeroOfNonzeroPhi hœÜ
  unfold Chart InverseChart
  set x := Projectivization.mk ùïú (v + u.1) hvu
  have hx : x ‚àà Goodset œÜ := by
    rw [‚ÜêGoodsetPreimage]
    exact hœÜ
  have heq : ‚Üë(ContinuousRetractionOnHyperplane hv ((1 / œÜ x.rep) ‚Ä¢ x.rep - v)) = ((1 / œÜ x.rep) ‚Ä¢ x.rep - v) := by
    rw [ContinuousRetractionIsRetraction hv ‚ü®_, ChartCodomainEqDomainInverseChart hv hx‚ü©]
  rw [hv, ‚ÜêSetCoe.ext_iff, heq]
  have hsimp : (1 / (œÜ (Projectivization.rep x))) ‚Ä¢ (Projectivization.rep x) = v + u := by
    have ha := @Projectivization_vs_LinearMap_cor ùïú E _ _ _ œÜ _ _ (Projectivization.rep_nonzero x) hvu
    erw [hœÜ1] at ha
    simp only [Projectivization.mk_rep, ContinuousLinearMap.coe_coe,  ne_eq, one_ne_zero, not_false_eq_true,
      div_self, smul_add, one_smul, forall_true_left] at ha
    exact ha
  rw [hsimp]
  simp only [add_sub_cancel']

end Charts_are_inverses


/- Charted space structure on ‚Ñô(E). -/

section ChartAsPartialHomeomorph


/- Definition of the Partial homeomorphisms between ‚Ñô(E) and the hyperplanes. -/

def Chart_PartialEquiv {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv: œÜ v = 1) : PartialEquiv (‚Ñô ùïú E) (LinearMap.ker œÜ) :=
{
  toFun := Chart hv
  invFun := InverseChart hv
  source := Goodset œÜ
  target := ‚ä§
  map_source' := by tauto
  map_target' := fun u _ => InverseChart_CodomainGoodset hv u
  left_inv' := fun _ hx => InverseChartChart hv hx
  right_inv' := fun u _ => ChartInverseChart hv u
}


def Chart_PartialHomeomorph {œÜ : E ‚ÜíL[ùïú] ùïú} {v : E} (hv: œÜ v = 1) :
PartialHomeomorph (‚Ñô ùïú E) (LinearMap.ker œÜ) := {Chart_PartialEquiv hv with
  open_source := GoodsetIsOpen œÜ
  open_target := isOpen_univ
  continuousOn_toFun := Chart_IsContinuous hv
  continuousOn_invFun := Continuous.continuousOn (InverseChart_IsContinuous hv)
}

end ChartAsPartialHomeomorph

/- Here is how change of charts works if we use charts with varying codomaons:
We consider two charts, defined by œÜ‚ÇÅ, v‚ÇÅ and œÜ‚ÇÇ, v‚ÇÇ respectively. The change of
chart map goes from Ker œÜ‚ÇÅ to Ker œÜ‚ÇÇ, it is defined on the open set
{u : Ker œÜ‚ÇÅ | œÜ‚ÇÇ(v‚ÇÅ+u)‚â†0}, and it sends u to (œÜ‚ÇÇ(v‚ÇÇ)/œÜ‚ÇÇ(v‚ÇÅ+u))‚Ä¢(v‚ÇÅ+u)-v‚ÇÇ. This is
always smooth, even if E is infinite-dimensional.
For charts with fixed codomain, it's the same but we throw in some continuous linear
equivalences at the beginning and at the end, and these are smooth.
Note that we need ùïú to be complete to prove smoothness of quotients of smooth functions. -/

section ChangeOfChart


/- The version with varying codomains.-/

def ChangeOfChart' {œÜ œà : E ‚ÜíL[ùïú] ùïú} {v w : E} (hv : œÜ v = 1) (hw : œà w = 1) :
LinearMap.ker œà ‚Üí LinearMap.ker œÜ := (Chart hv) ‚àò (InverseChart hw)

lemma ChangeOfChart'_formula {œÜ œà : E ‚ÜíL[ùïú] ùïú} {v w : E} (hv : œÜ v = 1) (hw : œà w = 1)
(u : LinearMap.ker œà) :
ChangeOfChart' hv hw u = ContinuousRetractionOnHyperplane hv ((1 / (œÜ (w + u))) ‚Ä¢ (w + u) - v) := by
  unfold ChangeOfChart' Chart InverseChart
  simp only [Function.comp_apply, map_sub, sub_left_inj]
  apply congrArg
  rw [hv]
  have hwu : w + u.1 ‚â† 0 := by
    have h : œà (w + u.1) ‚â† 0 := by
      rw [map_add, hw, u.2, add_zero]
      exact one_ne_zero
    exact NonzeroOfNonzeroPhi h
  exact Projectivization_vs_LinearMap_cor (œÜ : E ‚Üí‚Çó[ùïú] ùïú) (Projectivization.rep_nonzero _) hwu
    (Projectivization.mk_rep _ )


lemma ChangeOfChart'_IsSmoothOn {œÜ œà : E ‚ÜíL[ùïú] ùïú} {v w : E} (hv : œÜ v = 1) (hw : œà w = 1) :
ContDiffOn ùïú ‚ä§ (ChangeOfChart' hv hw) {u : LinearMap.ker œà | œÜ (w + u) ‚â† 0} := by
  refine ContDiffOn.congr ?_ (fun u _ => ChangeOfChart'_formula hv hw u)
  apply ContDiffOn.continuousLinearMap_comp
  apply ContDiffOn.sub
  . apply ContDiffOn.smul
    . simp_rw [one_div]
      apply ContDiffOn.inv
      . apply ContDiffOn.continuousLinearMap_comp
        apply ContDiffOn.add
        . apply contDiffOn_const
        . apply ContDiff.contDiffOn
          apply ContinuousLinearMap.contDiff (Submodule.subtypeL (LinearMap.ker œà))
      . simp only [map_add, ne_eq, Set.mem_setOf_eq, imp_self, Subtype.forall, LinearMap.mem_ker, implies_true,
        forall_const]
    . apply ContDiffOn.add
      . apply contDiffOn_const
      . apply ContDiff.contDiffOn
        apply ContinuousLinearMap.contDiff (Submodule.subtypeL (LinearMap.ker œà))
  . apply contDiffOn_const


/- The version with fixed codomain.-/

variable {œá : E ‚ÜíL[ùïú] ùïú} (hœá : œá ‚â† 0)

def ChangeOfChart {œÜ œà : E ‚ÜíL[ùïú] ùïú} {v w : E} (hv : œÜ v = 1) (hw : œà w = 1) :
LinearMap.ker œá ‚Üí LinearMap.ker œá :=
(OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hv) hœá) ‚àò (Chart hv) ‚àò (InverseChart hw) ‚àò
(OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hw) hœá).symm


lemma ChangeOfChart_IsSmoothOn {œÜ œà : E ‚ÜíL[ùïú] ùïú} {v w : E} (hv : œÜ v = 1) (hw : œà w = 1) :
ContDiffOn ùïú ‚ä§ (ChangeOfChart hœá hv hw)
((OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hw) hœá)''
{u : LinearMap.ker œà | œÜ (w + u) ‚â† 0}) := by
  unfold ChangeOfChart
  refine @ContDiffOn.continuousLinearMap_comp ùïú _ (LinearMap.ker œá) _ _ (LinearMap.ker œÜ) _ _ (LinearMap.ker œá) _ _
    _ _ ‚ä§ (OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hv) hœá) ?_
  rw [‚ÜêFunction.comp.assoc]
  change ContDiffOn ùïú ‚ä§ (_ ‚àò (OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hw) hœá).invFun) _
  have heq : ‚àÄ (s : Set (LinearMap.ker œà)),
    ((OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hw) hœá))'' s =
    (OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hw) hœá).invFun ‚Åª¬π' s := by
    intro s
    rw [Set.image_eq_preimage_of_inverse]
    . exact (OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hw) hœá).left_inv
    . exact (OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hw) hœá).right_inv
  rw [heq _]
  refine @ContDiffOn.comp_continuousLinearMap ùïú _ (LinearMap.ker œà) _ _ (LinearMap.ker œÜ) _ _ (LinearMap.ker œá) _ _  _ _ ‚ä§ ?_
    ((OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hw) hœá).symm : _ ‚ÜíL[ùïú] _)
  exact ChangeOfChart'_IsSmoothOn hv hw


lemma ChangeOfChartIsChangeOfChart {œÜ œà : E ‚ÜíL[ùïú] ùïú} {v w : E} (hv : œÜ v = 1) (hw : œà w = 1) :
(‚Üë(PartialHomeomorph.trans (PartialHomeomorph.symm (PartialHomeomorph.transHomeomorph (Chart_PartialHomeomorph hv)
(ContinuousLinearEquiv.toHomeomorph (OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hv) hœá))))
(PartialHomeomorph.transHomeomorph (Chart_PartialHomeomorph hw) (ContinuousLinearEquiv.toHomeomorph
(OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hw) hœá))))) =
ChangeOfChart hœá hw hv := by
  unfold Chart_PartialHomeomorph Chart_PartialEquiv ChangeOfChart
  ext u
  simp only [Set.top_eq_univ, PartialHomeomorph.coe_trans, PartialHomeomorph.transHomeomorph_apply,
           ContinuousLinearEquiv.coe_toHomeomorph, PartialHomeomorph.mk_coe, PartialHomeomorph.transHomeomorph_symm_apply,
           PartialHomeomorph.mk_coe_symm, PartialEquiv.coe_symm_mk, ContinuousLinearEquiv.symm_toHomeomorph,
           Function.comp_apply]


lemma ChangeOfChart_domain {œÜ œà : E ‚ÜíL[ùïú] ùïú} {v w : E} (hv : œÜ v = 1) (hw : œà w = 1) :
(PartialHomeomorph.trans (PartialHomeomorph.symm (PartialHomeomorph.transHomeomorph (Chart_PartialHomeomorph hv)
(ContinuousLinearEquiv.toHomeomorph (OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hv) hœá))))
(PartialHomeomorph.transHomeomorph (Chart_PartialHomeomorph hw) (ContinuousLinearEquiv.toHomeomorph
(OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hw) hœá)))).toPartialEquiv.source =
((OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hv) hœá)''
{u : LinearMap.ker œÜ | œà (v + u) ‚â† 0}) := by
  simp only [PartialHomeomorph.trans_toPartialEquiv, PartialHomeomorph.symm_toPartialEquiv, PartialEquiv.trans_source,
    PartialEquiv.symm_source, PartialHomeomorph.transHomeomorph_target, ContinuousLinearEquiv.symm_toHomeomorph,
    ContinuousLinearEquiv.coe_toHomeomorph, PartialHomeomorph.coe_coe_symm, PartialHomeomorph.transHomeomorph_symm_apply,
    PartialHomeomorph.transHomeomorph_source, map_add]
  unfold Chart_PartialHomeomorph Chart_PartialEquiv
  simp only [Set.top_eq_univ, Set.preimage_univ, PartialHomeomorph.mk_coe_symm, PartialEquiv.coe_symm_mk, Set.univ_inter]
  ext u
  simp only [Set.mem_preimage, Function.comp_apply, Set.mem_setOf_eq, Subtype.exists, LinearMap.mem_ker,
    exists_and_left]
  unfold InverseChart
  rw [‚ÜêGoodsetPreimage]
  simp only [map_add, Set.mem_image, Set.mem_setOf_eq, Subtype.exists, LinearMap.mem_ker, exists_and_left]
  constructor
  . intro h
    existsi (OneIsomorphismBetweenTwoClosedHyperplanes (NonzeroPhiOfPhiEqOne hv) hœá).symm u
    simp only [ne_eq, h, not_false_eq_true, Subtype.coe_eta, ContinuousLinearEquiv.apply_symm_apply,
      LinearMap.map_coe_ker, exists_prop, and_self]
  . intro h
    match h with
    | ‚ü®a, ha‚ü© =>
      match ha.2 with
      | ‚ü®x, hx‚ü© =>
        rw [‚Üêhx]
        simp only [ContinuousLinearEquiv.symm_apply_apply, ne_eq, ha.1, not_false_eq_true]

  end ChangeOfChart

end ProjectiveSpace
